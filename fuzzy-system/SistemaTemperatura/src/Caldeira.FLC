FUNCTION_BLOCK Caldeira    // In�cio do bloco de defini��es
   
      VAR_INPUT               // Defini��o das vari�veis de entrada
          Temperatura: REAL;
          Volume: REAL;
      END_VAR
   
      VAR_OUTPUT              // Defini��o das vari�veis de sa�da
          Pressao: REAL;
      END_VAR

      FUZZIFY Temperatura       
          // defini��o das parti��es fuzzy e seus intervalos para cada vari�vel de entrada
          TERM BAIXA := (800.0, 0) (800.0, 1) (900.0, 1) (1000.0, 0) ;
	  TERM MEDIA := (900.0, 0) (1000.0, 1) (1100.0, 0) ;
          TERM ALTA  := (1000.0, 0) (1100.0, 1) (1200.0, 1) (1200.0, 0) ;
      END_FUZZIFY

      FUZZIFY Volume       
          // defini��o das parti��es fuzzy e seus intervalos para cada vari�vel de entrada
          TERM PEQUENO := (2.0, 0) (2.0, 1) (4.5, 1) (7.0, 0) ;
	  TERM MEDIO   := (4.5, 0) (7.0, 1) (9.5, 0) ;
          TERM GRANDE    := (7.0, 0) (9.5, 1) (12.0, 1) (12.0, 0) ;
      END_FUZZIFY 
  
      DEFUZZIFY Pressao
          // defini��o das parti��es fuzzy e seus intervalos para cada vari�vel de sa�da

          TERM BAIXA := (4.0, 0) (4.0, 1) (5.0, 1) (8.0, 0) ;
	  TERM MEDIA := (6, 0) (8.0, 1) (10.0, 0) ;
          TERM ALTA  := (8.0, 0) (11.0, 1) (12.0, 1) (12.0, 0) ;
          METHOD : COG;  // M�todo de defuzzifica��o (Padr�o � o Centro de Gravidade)
          DEFAULT := 0;  // Valor default caso nenhuma regra seja ativada
      END_DEFUZZIFY

      RULEBLOCK No1
          // Defini��o do conjunto de regras para o controlador Fuzzy. Este bloco ir� descrever
          // as correla��es entre as parti��es da vari�vel de entrada com uma parti��o da vari�vel
          // de sa�da
          
          AND : MIN;  // M�todo MIN utilizado no processamento do operador l�gico AND
          ACT : MIN;  // M�todo de ativa��o
          ACCU : MAX; // m�todo de acumula��o
   
          // In�cio da descri��o de cada regra
          RULE 1 : IF Temperatura IS BAIXA AND Volume IS PEQUENO THEN Pressao IS BAIXA;
	  RULE 2 : IF Temperatura IS MEDIA AND Volume IS PEQUENO THEN Pressao IS BAIXA;
	  RULE 3 : IF Temperatura IS ALTA  AND Volume IS PEQUENO THEN Pressao IS MEDIA;
	  RULE 4 : IF Temperatura IS BAIXA AND Volume IS MEDIO   THEN Pressao IS BAIXA;
	  RULE 5 : IF Temperatura IS MEDIA AND Volume IS MEDIO   THEN Pressao IS MEDIA;
	  RULE 6 : IF Temperatura IS ALTA  AND Volume IS MEDIO   THEN Pressao IS ALTA ;
	  RULE 7 : IF Temperatura IS BAIXA AND Volume IS GRANDE  THEN Pressao IS MEDIA;
	  RULE 8 : IF Temperatura IS MEDIA AND Volume IS GRANDE  THEN Pressao IS ALTA ;
	  RULE 9 : IF Temperatura IS ALTA  AND Volume IS GRANDE  THEN Pressao IS ALTA ;
   
      END_RULEBLOCK
   
END_FUNCTION_BLOCK	
